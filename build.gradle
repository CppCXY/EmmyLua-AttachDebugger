/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.jetbrains.intellij' version "0.3.11"
    id "org.jetbrains.kotlin.jvm" version "1.2.31"
    id "de.undercouch.download" version "3.4.3"
}

intellij {
    pluginName 'EmmyLua-AttachDebugger'
    type 'IU'
    updateSinceUntilBuild false
    downloadSources false
    version = "IC-201.6668.121"
    localPath System.env["IDEA_HOME_201"]
    plugins 'com.tang:1.3.3.150-IDEA201'
}

sourceSets {
    main {
        java.srcDirs 'src'
        resources {
            srcDirs 'resources'
            exclude "debugger/**"
        }
    }
}

// Appveyor CI
if (System.env['APPVEYOR']) {
    if (System.env['APPVEYOR_REPO_TAG_NAME']) {
        version = System.env['APPVEYOR_REPO_TAG_NAME']
    } else {
        version = System.env['APPVEYOR_BUILD_VERSION']
    }
}

def RES_DIR = "resources"

task downloadEmmyDebugger(type: Download) {
    src ([
            "https://github.com/EmmyLua/EmmyLuaDebugger/releases/download/${emmyDebuggerVersion}/emmy_core@x64.zip",
            "https://github.com/EmmyLua/EmmyLuaDebugger/releases/download/${emmyDebuggerVersion}/emmy_core@x86.zip",
    ])
    dest "temp"
}

task unzipEmmyDebugger(type: Copy, dependsOn: downloadEmmyDebugger) {
    from(zipTree("temp/emmy_core@x64.zip")) {
        into "x64"
    }
    from(zipTree("temp/emmy_core@x86.zip")) {
        into "x86"
    }
    destinationDir file("temp")
}

task installEmmyDebugger(dependsOn: unzipEmmyDebugger, type: Copy) {
    from("temp/x64/") {
        include "*.*"
        into "debugger/emmy/windows/x64"
    }
    from("temp/x86/") {
        include "*.*"
        into "debugger/emmy/windows/x86"
    }
    destinationDir file(RES_DIR)
}
buildPlugin.dependsOn([installEmmyDebugger])
buildPlugin.from(fileTree(dir: RES_DIR, includes: ['debugger/**'])) {
    into "/${project.name}/classes/"
}
buildPlugin.from(fileTree(dir: RES_DIR, include: '!!DONT_UNZIP_ME!!.txt')) {
    into "/${project.name}"
}